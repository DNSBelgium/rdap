plugins {
    id 'java'
    id 'net.researchgate.release' version '3.1.0'
    id 'org.sonarqube' version '6.3.1.5724'
    id 'com.vanniktech.maven.publish' version '0.34.0'
}

ext {
    guava_version = '32.1.3-jre'
    slf4j_version = '2.0.9'
    jackson_version = '2.15.3'
    spring_version = '6.0.13'

    lib = [
            icu4j                 : 'com.ibm.icu:icu4j:73.2',
            guava                 : "com.google.guava:guava:${guava_version}",
            commons_lang          : 'org.apache.commons:commons-lang3:3.13.0',
            commons_text          : 'org.apache.commons:commons-text:1.14.0',
            servlet_api           : 'jakarta.servlet:jakarta.servlet-api:6.0.0',
            joda_time             : 'joda-time:joda-time:2.12.5',
            junit                 : 'junit:junit:4.13.2',
            slf4j                 : "org.slf4j:slf4j-api:${slf4j_version}",
            jackson               : ["com.fasterxml.jackson.core:jackson-core:${jackson_version}", "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"],
            mockito               : 'org.mockito:mockito-core:5.5.0',
            jsonpath              : 'com.jayway.jsonpath:json-path:2.8.0',
            spring_core           : "org.springframework:spring-core:${spring_version}",
            spring_webmvc         : "org.springframework:spring-webmvc:${spring_version}",
            spring_web            : "org.springframework:spring-web:${spring_version}",
            spring_test           : "org.springframework:spring-test:${spring_version}",
            groovy                : 'org.codehaus.groovy:groovy:3.0.19',
    ]
}

allprojects {
    group = 'be.dnsbelgium'
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'com.vanniktech.maven.publish'

    repositories {
        mavenCentral()
    }

    test {
        //forkEvery = 5
        //maxParallelForks = Runtime.runtime.availableProcessors() - 1
        systemProperties 'file.encoding': 'UTF-8'
        testLogging {
            //exceptionFormat "full"
            //events 'started', 'passed', 'skipped', 'failed'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        //options.compilerArgs << "-Xlint:all"
    }

    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    mavenPublishing {
        publishToMavenCentral()

        signAllPublications()

        coordinates(group.toString(), project.name, version.toString())

        pom {
            name = 'RDAP ' + project.name
            description = 'RDAP implementation'
            url = 'https://github.com/DNSBelgium/rdap'
            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }
            developers {
                developer {
                    id = 'dnsbelgium'
                    name = 'DNS Belgium Engineering'
                    email = 'engineering@dnsbelgium.be'
                    organization = 'DNS Belgium'
                    organizationUrl = 'https://www.dnsbelgium.be'
                }
            }
            scm {
                connection = 'scm:git@github.com:DNSBelgium/rdap.git'
                developerConnection = 'scm:git@github.com:DNSBelgium/rdap.git'
                url = 'scm:git@github.com:DNSBelgium/rdap.git'
            }
        }
    }

    release {
        git {
            requireBranch.set('master')
        }
    }

    afterReleaseBuild.dependsOn publishToMavenCentral

}
